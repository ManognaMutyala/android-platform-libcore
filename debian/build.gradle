subprojects {
  apply plugin: 'java'
  apply plugin: 'maven'

  version = "android-$androidVersion"
  group = 'com.android'
  
  repositories {
    maven { url '/usr/share/maven-repo' }
  }

  task poms {
  outputs.files "${buildDir}/${project.name}.pom"
    pom {
      project {}
    }.writeTo("${buildDir}/${project.name}.pom")
  }

  clean.doLast {
    delete "${buildDir}/${project.name}.pom"
  }
}

project(':luni') {
  dependencies {
    compile 'com.ibm.icu:icu4j:debian'
    compile 'kxml2:kxml2:debian'
    compile 'xpp3:xpp3:debian'
    compile project(':dex')
  }

  sourceSets.main.java {
    srcDir '../dalvik/src/main/java'
    srcDir '../libart/src/main/java'
    srcDir '../xml/src/main/java'
  }

  jar.manifest {
    attributes 'Class-Path': configurations.compile.getDependencies().collect {
      if (it instanceof org.gradle.api.artifacts.ProjectDependency) {
        "/usr/share/java/com.android.${it.name}.jar"
      } else {
        "/usr/share/java/${it.name}.jar"
      }
    }.join(' ')
  }
}